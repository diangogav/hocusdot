# Set up the prompt

autoload -Uz promptinit
promptinit
# prompt adam1

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

# ============================================================================
# History Configuration
# ============================================================================
HISTSIZE=50000                    # Lines of history in memory
SAVEHIST=50000                    # Lines of history to save to file
HISTFILE=~/.zsh_history           # History file location

# History options
setopt EXTENDED_HISTORY           # Record timestamp of command in HISTFILE
setopt HIST_EXPIRE_DUPS_FIRST     # Delete duplicates first when HISTFILE size exceeds HISTSIZE
setopt HIST_IGNORE_DUPS           # Ignore duplicated commands history list
setopt HIST_IGNORE_SPACE          # Ignore commands that start with space
setopt HIST_VERIFY                # Show command with history expansion before running it
setopt INC_APPEND_HISTORY         # Add commands to HISTFILE immediately, not on shell exit
setopt SHARE_HISTORY              # Share command history data between sessions

# Dotfiles directory
export DOTFILES_PATH="$HOME/.dotfiles"

# Add scripts folder and local bin to PATH
export PATH="$HOME/.dotfiles/scripts:$HOME/.local/bin:$HOME/.local/share/fnm:$PATH"


# Bat as manpager (colored man pages)
export MANPAGER="sh -c 'col -bx | bat -l man -p'"
export MANROFFOPT="-c"

# Environment Variables

# source $DOTFILES_PATH/terminal/private-exports.sh

# Load aliases file
source $DOTFILES_PATH/terminal/aliases

# Load Starship
eval "$(starship init zsh)"

# load ZSH completions
source $DOTFILES_PATH/terminal/zsh/completion.zsh

# Load useful functions
source $DOTFILES_PATH/terminal/zsh/functions.zsh

# ============================================================================
# Zinit Plugin Manager
# ============================================================================
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"

# Install zinit if not present
if [[ ! -d "$ZINIT_HOME" ]]; then
   mkdir -p "$(dirname $ZINIT_HOME)"
   git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

# Load zinit
source "${ZINIT_HOME}/zinit.zsh"

# ============================================================================
# Plugins
# ============================================================================

# Load plugins with turbo mode (lazy loading)
zinit wait lucid for \
  atinit"zicompinit; zicdreplay" \
    zdharma-continuum/fast-syntax-highlighting \
  atload"_zsh_autosuggest_start" \
    zsh-users/zsh-autosuggestions \
  blockf atpull'zinit creinstall -q .' \
    zsh-users/zsh-completions

# History substring search (bind after loading)
zinit ice wait lucid atload'bindkey "^[[A" history-substring-search-up; bindkey "^[[B" history-substring-search-down'
zinit light zsh-users/zsh-history-substring-search

# You-should-use (reminds you of aliases)
zinit ice wait lucid
zinit light MichaelAquilina/zsh-you-should-use

# FZF-Tab (visual completion with fzf) - DISABLED for performance
# zinit ice wait lucid
# zinit light Aloxaf/fzf-tab

# FZF-Tab configuration - DISABLED
# zstyle ':fzf-tab:*' fzf-command fzf
# zstyle ':fzf-tab:*' fzf-flags --height=40% --border
# zstyle ':fzf-tab:complete:cd:*' fzf-preview ''
# zstyle ':fzf-tab:complete:*:*' fzf-preview 'bat --color=always --style=numbers --line-range=:50 $realpath 2>/dev/null'


# ============================================================================
# Modern Tools Integration
# ============================================================================

# FNM (Fast Node Manager) - Lazy loaded
[ -f $DOTFILES_PATH/terminal/nodejs/fnm.zsh ] && source $DOTFILES_PATH/terminal/nodejs/fnm.zsh

# Zoxide (smart cd replacement)
if command -v zoxide &> /dev/null; then
  eval "$(zoxide init zsh)"
fi

# Ollama AI (local AI assistant)
[ -f $DOTFILES_PATH/terminal/ai/ollama.zsh ] && source $DOTFILES_PATH/terminal/ai/ollama.zsh

# Direnv (automatic environment variables)
if command -v direnv &>/dev/null; then
  eval "$(direnv hook zsh)"
fi

# ZSH Configurations
set -o noclobber #ensure that shell redirection will not overwrite existing files

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh


_reverse_search() {
  local selected_command=$(fc -rl 1 | awk '{$1="";print substr($0,2)}' | fzf)
  LBUFFER=$selected_command
}

zle -N _reverse_search
bindkey '^r' _reverse_search

# NPM completion (lazy loaded)
[ -f $DOTFILES_PATH/terminal/nodejs/npm-completion.zsh ] && source $DOTFILES_PATH/terminal/nodejs/npm-completion.zsh


bindkey "^[[H" beginning-of-line
bindkey "^[[F" end-of-line
bindkey "^[[3~" delete-char
bindkey "^[[1;3C" forward-word
bindkey "^[[1;3D" backward-word

# ============================================================================
# Welcome Banner
# ============================================================================
welcome-banner
